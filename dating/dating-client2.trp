(* Placeholder for the dating-client. Note that in order to communicate 
   with your server you will need to create the corresponding 
   trustmap file *)
import lists
import declassifyutil
let
   val name = "Doe" raisedTo {doe}
   val year = 1919 raisedTo {doe}
   val gender = true raisedTo {doe}
   val interests = ["Donuts", "signatures"] raisedTo {doe}
   val thisprofile = ({doe}, name, year, gender, interests)
   fun genderPreference (lev, _, _, gender, _)  = if gender then (false, ()) else (true, declassifydeep(thisprofile, authority, lev))
   (*fun genderPreference (level, _, _, gender, _)  = if gender then (false, ()) else (true, thisprofile)*)
   
   fun receiveMatch matches = 
      let
         val profile = receive [hn ("NEWMATCH", profile )
                              => profile
                           ]
         val new_matches = profile::matches
         val _ = printWithLabels new_matches
      in
         receiveMatch new_matches
      end

   fun main () =
      let
         val p =  spawn (fn() => receiveMatch [])
         val thisNode = node (self ())
         val _ = printString ("Running node with identifier: " ^ thisNode)
         val _ = register("client2",  p, authority)
         val data = (thisprofile, genderPreference, p)
      in 
         send(whereis ("QmaWwd8W8reMEkkavJxDnthukzGq9Y5D1QFbJja2iquBbX", "datingServer"), ("NEWPROFILE", data))
      end


in
  main()
end